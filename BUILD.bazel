load("@rules_cc//cc:defs.bzl", "cc_library")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake_variant")

package(default_visibility = ["//visibility:public"])

cc_library(
    name = "api",
    hdrs = glob(["include/*.h"]),
    strip_include_prefix = "//include",
)

cmake_variant(
    name = "libevent",
    cache_entries = {
        "EVENT__DISABLE_OPENSSL": "off",
        "EVENT__DISABLE_MBEDTLS": "on",
        "EVENT__DISABLE_REGRESS": "on",
        "EVENT__DISABLE_TESTS": "on",
        "EVENT__LIBRARY_TYPE": "STATIC",
        # Force _GNU_SOURCE on for Android builds. This would be contained in
        # a 'select' but the downstream macro uses a select on all of these
        # options, and they cannot be nested.
        # If https://github.com/bazelbuild/rules_foreign_cc/issues/289 is fixed
        # this can be removed.
        # More details https://github.com/envoyproxy/envoy-mobile/issues/116
        "_GNU_SOURCE": "on",
    },
    deps = [
        "@openssl//:ssl",
    ],
    lib_source = "@libevent//:all_srcs",
    linkopts = [
        "-lstdc++",
    ],
    out_static_libs = select({
        # macOS organization of libevent is different from Windows/Linux.
        # Including libevent_core is a requirement on those platforms, but
        # results in duplicate symbols when built on macOS.
        # See https://github.com/envoyproxy/envoy-mobile/issues/677 for details.
        "@platforms//os:osx": [
            "libevent.a",
            "libevent_pthreads.a",
            "libevent_openssl.a",
        ],
        "@platforms//os:windows": [
            "event.lib",
            "event_core.lib",
            "event_openssl.lib",
        ],
        "//conditions:default": [
            "libevent.a",
            "libevent_pthreads.a",
            "libevent_core.a",
            "libevent_openssl.a",
        ],
    }),
    toolchain = "@rules_foreign_cc//toolchains:preinstalled_cmake_toolchain",
)

cc_library(
    name = "rocketmq_cpp_sdk",
    hdrs = glob(["src/**/*.h"]),
    srcs = glob(["src/**/*.cpp"], exclude = ["src/dllmain.cpp",],)
    + select({
        "@platforms//os:windows": ["src/dllmain.cpp",],
        "//conditions:default": [],
    }),
    copts = [
        "-Isrc/common",
        "-Isrc/consumer",
        "-Isrc/include",
        "-Isrc/log",
        "-Isrc/message",
        "-Isrc/producer",
        "-Isrc/protocol",
        "-Isrc/status",
        "-Isrc/thread",
        "-Isrc/trace",
        "-Isrc/transport",
        "-Isrc"
    ],
    deps = [
        ":api",
        ":libevent",
        "//libs/signature",
        "@boost//:thread",
        "@boost//:asio_ssl",
        "@boost//:log",
        "@boost//:locale",
        "@jsoncpp//:jsoncpp",
    ]
)